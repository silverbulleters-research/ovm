#Использовать "../../core"

Перем ЭтоWindows;

// Заполняет описание команды для библиотеки cli
//
// Параметры:
//   КомандаПриложения - КомандаПриложения - Настраиваемая команда
//
Процедура ОписаниеКоманды(КомандаПриложения) Экспорт
	
	КомандаПриложения
		.Аргумент(
			"VERSION",
			"current",
			"Используемое окружение OneScript. " +
				"Допустимо использовать трехномерные версии (1.0.17, 1.0.18), stable, dev, current")
		.ВОкружении("OVM_RUN_VERSION");
	
	КомандаПриложения
		.Аргумент("APP", "oscript", "Используемое приложение. Например, oscript, opm, vrunner...")
		.ВОкружении("OVM_RUN_APP");
	
	КомандаПриложения
		.Аргумент("ARGS", "", "Аргументы, передаваемые приложению").ТМассивСтрок()
		.ВОкружении("OVM_RUN_ARGS");
	
	// TODO: Убрать, после реализации https://github.com/khorevaa/cli/issues/10
	КомандаПриложения.Спек = "VERSION APP [-- ARGS...]";
	
КонецПроцедуры

// Обработчик выполнения команды
//
// Параметры:
//   КомандаПриложения - КомандаПриложения - Выполняемая команда
//
Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
	
	ИспользуемаяВерсия = КомандаПриложения.ЗначениеАргумента("VERSION");
	Приложение = КомандаПриложения.ЗначениеАргумента("APP");
	Аргументы = КомандаПриложения.ЗначениеАргумента("ARGS");

	КаталогУстановки = ПараметрыOVM.КаталогУстановкиПоУмолчанию();
	КаталогУстановкиВерсии = ОбъединитьПути(КаталогУстановки, ИспользуемаяВерсия);
	КаталогBin = ОбъединитьПути(КаталогУстановкиВерсии, "bin");
	
	ПеременнаяPATH = ПолучитьПеременнуюСреды("PATH");
	Если ЭтоWindows Тогда
		ПеременнаяPATH = КаталогBin + ";" + ПеременнаяPATH;
	Иначе
		ПеременнаяPATH = КаталогBin + ":" + ПеременнаяPATH;
	КонецЕсли;

	УстановитьПеременнуюСреды("PATH", ПеременнаяPATH, РасположениеПеременнойСреды.Процесс);
	
	Команда = Новый Команда;
	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	Команда.УстановитьКоманду(Приложение);
	Команда.ДобавитьПараметры(Аргументы);
	Команда.ПоказыватьВыводНемедленно(Истина);
	
	Команда.Исполнить();

КонецПроцедуры

СистемнаяИнформация = Новый СистемнаяИнформация;
ЭтоWindows = Найти(ВРег(СистемнаяИнформация.ВерсияОС), "WINDOWS") > 0;
